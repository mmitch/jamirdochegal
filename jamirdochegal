#!/usr/bin/perl
#
# jamirdochegal  -  listen to web radios via mplayer 
# Copyright (C) 2009  Christian Garbs <mitch@cgarbs.de>
# licensed under GNU GPL v2 (or later)
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 2 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;


#### process a line from the stations list
my %station;
sub process_station_line($)
{
    my $line = shift;

    chomp $line;
    return if $line =~ /^\s*$/;
    return if $line =~ /^\s*#/;

    if ($line =~ /^\s*DELETE:(\S+)\s*$/) {
	return "can't delete unkown station `$1'" unless exists $station{$1};
	delete $station{$1};

    } elsif ($line =~ /^\s*RENAME:(\S+)\s(\S+)*$/) {
	return "can't rename unkown station `$1'" unless exists $station{$1};
	$station{$2} = $station{$1};
	$station{$2}->{'SHORTCUT'} = $2;
	delete $station{$1};

    } elsif ($line =~ /^\s*(\S+)\|(.*)\s+(\S+)\s*$/) {
	$station{$1} = {
	    'SHORTCUT' => $1,
	    'NAME'     => $2,
	    'URL'      => $3,
	}

    } elsif ($line =~ /^\s*(.*)\s+(\S+)\s*$/) {
	$station{$1} = {
	    'SHORTCUT' => $1,
	    'NAME'     => $1,
	    'URL'      => $2,
	}
	
    } else {
	return "broken line";
    }
    return "";
}

#### read global stations
while (my $line = <DATA>) {
    my $err = process_station_line($line);
    warn "global configuration [$.]: $err\n" if $err;
}


#### read local stations
my $rcfile = $ENV{HOME}.'/.jamirdochegalrc';
if ( -e $rcfile ) {
    open RCFILE, '<', $rcfile or warn "can't open `$rcfile': $!\n";
    while (my $line = <RCFILE>) {
	my $err = process_station_line($line);
	warn "local configuration [$.]: $err\n" if $err;
    }
    close RCFILE or warn "can't open `$rcfile': $!\n";
}


#### default action
if (@ARGV == 0) {
    push @ARGV, '-r';
}


#### handle actions
my $action = shift @ARGV;


#### print help?
if ($action eq '-h') {
    print << "EOF" ;
jamirdochegal  -  listen to web radios via mplayer 

usage:
  jamirdochegal [-h | -l | -r | <station> [<additional-mplayer-arguments>]]
-h   print this help
-l   list all stations
-r   tune into a random station
default with no arguments given is -r
EOF
    ;
    exit 0;
}


#### print station list?
if ($action eq '-l') {
    foreach my $station (sort keys %station) {
	if ($station eq $station{$station}->{'NAME'}) {
	    print "$station\n";
	} else {
	    print "$station  ($station{$station}->{'NAME'})\n";
	}
    }
    exit 0;
}


#### random station?
if ($action eq '-r') {
    my @station = keys %station;
    $action = @station[ int rand (@station) ];
}


#### tune in
unless (exists $station{$action}) {
    my @matches = grep { substr($_, 0, length $action) eq $action } sort keys %station;
    if (@matches == 0) {
	die "unknown station `$action'\n";
    } elsif (@matches > 1) {
	warn "inexact station name `$action'\n";
	warn "possible completions:\n";
	foreach my $station (@matches) {
	    if ($station eq $station{$station}->{'NAME'}) {
		print "$station\n";
	    } else {
		print "$station  ($station{$station}->{'NAME'})\n";
	    }
	}
	exit 0;
    }
    $action = $matches[0];
}
print "\n>>>> $station{$action}->{NAME}\n\n";
my $url = $station{$action}->{URL};
my @parms = ( '-cache', '128' );
push @parms, '-playlist' unless $url =~ s/^DIRECT://;


#### and play
exec 'mplayer', @parms, $url, @ARGV;


#### the station list
# - define stations:
#   <name> <url>
#   <shortcut>|<name> <url>
#   where <url> is a playlist (otherwise prepend with 'DIRECT:')
# - delete stations:
#   DELETE:<name>
#   DELETE:<shortcut>
# - rename stations:
#   RENAME:<name> <shortcut>
#   RENAME:<shortcut> <shortcut>
__DATA__
bitjam http://hangar18.gotdns.org:8086/bitjam.ogg.m3u
demovibes http://demovibes.de:8000/necta192.mp3.m3u
dlf|deutschlandfunk http://www.dradio.de/streaming/dlf_hq_ogg.m3u
hardrave.jp http://hardrave.jp:9000/listen.pls
kohina http://relay.radio.ethz.ch:8000/kohina.ogg.m3u
nsw-anime DIRECT:http://217.115.150.152:8000/
ocremix|ormgas http://ormgas.rainwave.cc/tunein-oc.php?relay=se
sid|sid station http://sc1uk.myshoutserver.com/tunein.php/drbongo/playlist.pls
slayradio http://www.slayradio.org/tune_in.php/128kbps/listen.m3u
ssl|sunshine live http://lsd.newmedia.tiscali-business.com/bb/redirect.lsc?adid=0&content=live&media=mp3&stream=sunshinelive/livestream.mp3
sst|streaming soundtracks http://loudcity.com/stations/streamingsoundtracks-com/files/show/Ogg-hd.m3u
vgamp http://vgamp.com/listen128.pls
# needs authentication:
# cryosleep http://207.200.96.225:8022/listen.pls
# b0rken/recheck URL:
# cpc|cpc scene http://radio.cpcscene.com/cpcsceneradio.pls
