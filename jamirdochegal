#!/usr/bin/perl
#
# jamirdochegal  -  listen to web radios via mplayer 
# Copyright (C) 2009-2014  Christian Garbs <mitch@cgarbs.de>
# Licensed under GNU GPL v2 (or later)
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 2 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;


#### process a line from the stations list
my %station;
sub process_station_line($)
{
    my $line = shift;

    chomp $line;
    return if $line =~ /^\s*$/;
    return if $line =~ /^\s*#/;

    if ($line =~ /^\s*DELETE:(\S+)\s*$/) {
	return "can't delete unkown station `$1'" unless exists $station{$1};
	delete $station{$1};

    } elsif ($line =~ /^\s*RENAME:(\S+)\s(\S+)*$/) {
	return "can't rename unkown station `$1'" unless exists $station{$1};
	$station{$2} = $station{$1};
	$station{$2}->{'SHORTCUT'} = $2;
	delete $station{$1};

    } elsif ($line =~ /^(?:\s*(\S+)\|)?(.*)\s+(\S+)\s*$/) {
	my ($shortcut, $name, $url, $flags) = ($1, $2, $3, {});

	$shortcut = $name unless defined $shortcut;

	while ($url =~ s/^([A-Z]+)://g) {
	    $flags->{$1}++;
	}

	$station{$shortcut} = {
	    'SHORTCUT' => $shortcut,
	    'NAME'     => $name,
	    'URL'      => $url,
	    'FLAGS'    => $flags,
	};
	
    } else {
	return "broken line";
    }
    return "";
}

#### read global stations
while (my $line = <DATA>) {
    my $err = process_station_line($line);
    warn "global configuration [$.]: $err\n" if $err;
}


#### read local stations
my $rcfile = $ENV{HOME}.'/.jamirdochegalrc';
if ( -e $rcfile ) {
    open RCFILE, '<', $rcfile or warn "can't open `$rcfile': $!\n";
    while (my $line = <RCFILE>) {
	my $err = process_station_line($line);
	warn "local configuration [$.]: $err\n" if $err;
    }
    close RCFILE or warn "can't open `$rcfile': $!\n";
}


#### default action
if (@ARGV == 0) {
    push @ARGV, '-r';
}


#### handle actions
my $action = shift @ARGV;


#### print help?
if ($action eq '-h') {
    print << "EOF" ;

jamirdochegal  -  listen to web radios via mplayer 
Copyright (C) 2009-2013  Christian Garbs <mitch\@cgarbs.de>
Licensed under GNU GPL v2 (or later)

usage:
  jamirdochegal [-h | -l | -r | <station> [<additional-mplayer-arguments>]]

  -h   print this help
  -l   list all stations
  -r   tune into a random station (default with no arguments given)

EOF
    ;
    exit 0;
}


#### print station list?
if ($action eq '-l') {
    foreach my $station (sort keys %station) {
	if ($station eq $station{$station}->{'NAME'}) {
	    print "$station\n";
	} else {
	    print "$station  ($station{$station}->{'NAME'})\n";
	}
    }
    exit 0;
}


#### random station?
if ($action eq '-r') {
    my @station = keys %station;
    $action = @station[ int rand (@station) ];
}


#### tune in
unless (exists $station{$action}) {
    my @matches = grep { substr($_, 0, length $action) eq $action } sort keys %station;
    if (@matches == 0) {
	die "unknown station `$action'\n";
    } elsif (@matches > 1) {
	warn "inexact station name `$action'\n";
	warn "possible completions:\n";
	foreach my $station (@matches) {
	    if ($station eq $station{$station}->{'NAME'}) {
		print "$station\n";
	    } else {
		print "$station  ($station{$station}->{'NAME'})\n";
	    }
	}
	exit 0;
    }
    $action = $matches[0];
}
print "\n>>>> $station{$action}->{NAME}\n\n";
my @parms = ( '-cache', '128' );
push @parms, ('-demuxer', 'ogg') if exists $station{$action}->{FLAGS}->{OGGDEMUX};
push @parms, '-playlist' unless exists $station{$action}->{FLAGS}->{DIRECT};

#### and play
exec 'mplayer', @parms, $station{$action}->{URL}, @ARGV;


#### the station list
# - define stations:
#   [<shortcut>|]<name> [<flag>]<url>
#   where <url> is a playlist
#   <flag> is optional an can be one or more of:
#   DIRECT:    when <url> is no playlist but a direct stream
#   OGGDEMUX:  work around mplayer/lavf-demuxer problems with OGG streams
# - delete stations:
#   DELETE:<name>
#   DELETE:<shortcut>
# - rename stations:
#   RENAME:<name> <shortcut>
#   RENAME:<shortcut> <shortcut>
__DATA__
bassdrive http://bassdrive.com/v2/streams/BassDrive.pls
dfmw|detektorfm-wort http://detektor.fm/detektor_fm-wort.m3u
dfmm|detektorfm-musik http://detektor.fm/detektor_fm-musik.m3u
di-classic http://www.di.fm/mp3/classictechno.pls
dlf|deutschlandfunk OGGDEMUX:http://www.dradio.de/streaming/dlf_hq_ogg.m3u
dradiowissen OGGDEMUX:http://www.dradio.de/streaming/dradiowissen_hq_ogg.m3u
klassiklounge DIRECT:http://edge.live.mp3.mdn.newmedia.nacamar.net/klassikradiolounge128/livestream.mp3
klassikmovie DIRECT:http://edge.live.mp3.mdn.newmedia.nacamar.net/klassikradiomovie128/livestream.mp3
klassikopera DIRECT:http://edge.live.mp3.mdn.newmedia.nacamar.net/klassikradioopera128/livestream.mp3
klassikradio DIRECT:http://edge.live.mp3.mdn.newmedia.nacamar.net/klassikradio128/livestream.mp3
kohina OGGDEMUX:http://kohina.radio.ethz.ch:8000/kohina.ogg.m3u
nsw-anime DIRECT:http://listen.nsw-anime.de/
nubreaks http://nubreaks.com/modules/mod_internetradio/makeplaylist.php?ip=69.175.103.232&port=8700&format=PLS
ocremix|ormgas http://ormgas.rainwave.cc/tunein.php
r7|radioseven http://www.radioseven.se/128.pls
sid|sid station http://streams4.invertedhost.com:2199/tunein/jotayl01.pls
slay|slayradio http://www.slayradio.org/tune_in.php/128kbps/listen.m3u
soma-beatblender http://somafm.com/startstream=beatblender.pls
soma-bootliquor http://somafm.com/startstream=bootliquor.pls
soma-cliqhop http://somafm.com/startstream=cliqhop.pls
soma-covers http://somafm.com/startstream=covers.pls
soma-digitalis http://somafm.com/startstream=digitalis.pls
soma-doomed http://somafm.com/startstream=doomed.pls
drone|soma-dronezone http://somafm.com/startstream=dronezone.pls
soma-groovesalad http://somafm.com/startstream=groovesalad.pls
soma-illstreet http://somafm.com/startstream=illstreet.pls
ip|soma-indiepop http://somafm.com/startstream=indiepop.pls
soma-lush http://somafm.com/startstream=lush.pls
mc|soma-missioncontrol http://somafm.com/startstream=missioncontrol.pls
soma-poptron http://somafm.com/startstream=poptron.pls
sa|soma-secretagent http://somafm.com/startstream=secretagent.pls
soma-sonicuniverse http://somafm.com/startstream=sonicuniverse.pls
soma-suburbsofgoa http://somafm.com/startstream=suburbsofgoa.pls
soma-tags http://somafm.com/startstream=tags.pls
soma-u80s http://somafm.com/startstream=u80s.pls
#soma-xmasinfrisco http://somafm.com/startstream=xmasinfrisco.pls
phish|live Phish radio http://www.livephish.com/radio/listen.m3u
sst|streaming soundtracks http://loudcity.com/stations/streamingsoundtracks-com/files/show/aacplus-hi.asx
subpop OGGDEMUX:DIRECT:http://pushnpop.net:8912/subpop.ogg
ctfm|ClubTime.FM http://listen.clubtime.fm/dsl.pls
htfm|HouseTime.FM http://listen.housetime.fm/dsl.pls
tbfm|TranceBase.FM http://listen.trancebase.fm/dsl.pls
tt24|Turntables24.com http://stream.turntables24.de:9200/listen.pls
ttfm|Trance.Techno.FM http://techno.fm/m3u/techno.mp3.m3u
ltfm|Live.Techno.FM http://techno.fm/m3u/live.mp3.m3u

# bitjam DIRECT:OGGDEMUX:http://88.80.10.21:8086/bitjam.ogg
# cryosleep http://207.200.96.225:8022/listen.pls
# hardrave.jp http://hardrave.jp:9000/listen.pls
# vgamp http://vgamp.com/listen128.pls
# rivendell http://www.radiorivendell.com/lib/blocks/rr_play.php?play_f=play&bitrate=128&codec=mp3&player=Winamp
# demovibes http://demovibes.de:8000/necta192.mp3.m3u
# ssl|sunshine live http://lsd.newmedia.tiscali-business.com/bb/redirect.lsc?adid=0&content=live&media=mp3&stream=sunshinelive/livestream.mp3
